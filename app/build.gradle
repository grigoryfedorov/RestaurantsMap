apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "org.grigoryfedorov.restaurantsmap"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    def getLocalPropertiesParam = { propertyName ->
        def localProperties = new Properties()
        localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
        localProperties.getProperty(propertyName)
    }

    buildTypes {
        def googleMapsKey = "google_maps_key"

        debug {
            it.resValue 'string', googleMapsKey, getLocalPropertiesParam('google.map.key.debug')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            it.resValue 'string', googleMapsKey, getLocalPropertiesParam('google.map.key.release')
        }
    }

    buildTypes.each {
        it.resValue 'string', "foursquare_client_id_key", getLocalPropertiesParam('foursquare.client.IdKey')
        it.resValue 'string', "foursquare_client_secret_key", getLocalPropertiesParam('foursquare.client.SecretKey')
    }


    lintOptions {
        abortOnError true
        warningsAsErrors true
    }

    android {
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    viewBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6"

    implementation 'androidx.appcompat:appcompat:1.1.0'

    // for new activity result and permissions API
    implementation "androidx.fragment:fragment-ktx:1.3.0-alpha04"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // viewmodelscope for coroutines
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'

    implementation 'com.squareup.okhttp3:okhttp:4.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation "io.mockk:mockk:1.9"
}
